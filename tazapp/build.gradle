def versionMajor = 3
def versionMinor = 7
def versionPatch = 0
def versionBuild = 1

def supportLibVer = "28.0.0"
def buildToolsVer = "28.0.3"
def compileSdkVer = 28
def targetSdkVer = 28
def minSdkVer = 15

def apkUrl = '"https://dl.taz.de/down/apk/"'

def tazPlist = '"https://dl.taz.de/tpaperIssue.plist"'
def tazPlistArchiv = '"https://dl.taz.de/tpaperIssue.plist?start=%1$s&end=%2$s"'
def tazStagingPlist = '"https://dl.taz.de/tpaperIssueTest.plist"'
def tazStagingPlistArchiv = '"https://dl.taz.de/tpaperIssueTest.plist?start=%1$s&end=%2$s"'
def tazAboUrl = '"https://www.taz.de/zeitung/abo/digitales-abo/apaper"'
def tazCheckLogin = '"https://dl.taz.de/digiAboCheckLogin"'
def tazResourceUrl = '"https://dl.taz.de/ressourcen"'
def tazArchiveUrl = '"https://dl.taz.de/tpaperArchiv"'
def tazPushRestUrl = '"https://dl.taz.de/tpaperPushInfo"'
def tazStagingPushRestUrl = '"https://dl.taz.de/tpaperPushInfoTest"'
def tazErrorMail = '"app@taz.de"'

def lmdPlist = '"https://dl.monde-diplomatique.de/tpaperIssue.plist"'
def lmdPlistArchiv = '"https://dl.monde-diplomatique.de/tpaperIssue.plist?start=%1$s&end=%2$s"'
def lmdStagingPlist = '"https://dl.monde-diplomatique.de/tpaperIssueTest.plist"'
def lmdStagingPlistArchiv = '"https://dl.monde-diplomatique.de/tpaperIssueTest.plist?start=%1$s&end=%2$s"'
def lmdAboUrl = '"https://monde-diplomatique.de/abo-digital"'
def lmdCheckLogin = '"https://dl.monde-diplomatique.de/digiAboCheckLogin"'
def lmdResourceUrl = '"https://dl.monde-diplomatique.de/ressourcen"'
def lmdArchiveUrl = '"https://dl.monde-diplomatique.de/tpaperArchiv"'
def lmdPushRestUrl = '"https://dl.monde-diplomatique.de/tpaperPushInfo"'
def lmdStagingPushRestUrl = '"https://dl.monde-diplomatique.de/tpaperPushInfoTest"'
def lmdErrorMail = '"app@monde-diplomatique.de"'

def plistUrl = "PLISTURL"
def plistArchiveUrl = "PLISTARCHIVURL"
def aboUrl = "ABOURL"
def checkLoginUrl = "CHECKLOGINURL"
def resourceUrl = "RESOURCEURL"
def archiveUrl = "ARCHIVEURL"
def pushRestUrl = "PUSHRESTURL"
def errorMail = "ERRORMAIL"

apply plugin: 'idea'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

buildscript {
    repositories {
        maven {
            url 'https://maven.fabric.io/public'
        }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.akaita.android.easylauncher'
apply plugin: 'com.github.triplet.play'


def overheadPath

File tcLocalPropsFile = project.rootProject.file('tc_local.properties')
if (tcLocalPropsFile.exists()) {
    logger.quiet('Found ' + tcLocalPropsFile)
    def tcLocalProps = new Properties()
    tcLocalProps.load(tcLocalPropsFile.newDataInputStream())
    overheadPath = tcLocalProps.getProperty("overheadpath")
} else {
    logger.error('tc_local.properties not found')
}

if (overheadPath == null) {
    overheadPath = System.getenv("overheadpath")
}

if (overheadPath != null) {
    logger.quiet('Found ' + ":" + overheadPath)
    project.ext.set("overheadPath", overheadPath)
    File extraGradleFile = new File(overheadPath, project.name + ".gradle")
    if (extraGradleFile.exists()) {
        logger.quiet('Found ' + extraGradleFile)
        apply from: extraGradleFile
    } else {
        logger.error("${extraGradleFile} not found")
    }
} else {
    logger.error('overheadpath not set')
}

easylauncher {
    iconNames "@mipmap/ic_launcher_foreground" // Traditional launcher icon
    foregroundIconNames "@mipmap/ic_launcher_foreground" // Foreground of adaptive launcher icon
    buildTypes {
        debug {
            filters = redRibbonFilter()
        }
        staging {
            filters = blueRibbonFilter()
        }
        release {
            enable false
        }
    }
}

android {

    sourceSets {
        staging {
            java {
                srcDir 'src/debug/java'
            }
        }
    }

    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer
    defaultConfig {
        resConfigs "en", "de"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex        : 'de.thecode.android.tazreader.eventbus.EventBusIndex',
                             "room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        buildConfigField 'String', 'APKURL', apkUrl
    }
    splits {
        abi {
            // Enables building multiple APKs per ABI.
            enable true

            // By default all ABIs are included, so use reset() and include to specify that we only
            // want APKs for x86, armeabi-v7a, and mips.
            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()
            // Specifies a list of ABIs that Gradle should create APKs for.
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"

            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk true

        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        debug {
            debuggable true
            versionNameSuffix " Debug"
            applicationIdSuffix ".debug"
            ext.enableCrashlytics = false
        }
        staging.initWith(buildTypes.debug)
        staging {
            versionNameSuffix " Staging"
            applicationIdSuffix ".staging"
            ext.enableCrashlytics = true
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    flavorDimensions "publication"

    productFlavors {
        taz {
            dimension "publication"
            applicationId "de.thecode.android.tazreader"
            resValue 'string', 'app_name', 'taz.app'
            buildConfigField 'String', plistUrl, tazPlist
            buildConfigField 'String', plistArchiveUrl, tazPlistArchiv
            buildConfigField 'String', aboUrl, tazAboUrl
            buildConfigField 'String', checkLoginUrl, tazCheckLogin
            buildConfigField 'String', resourceUrl, tazResourceUrl
            buildConfigField 'String', archiveUrl, tazArchiveUrl
            buildConfigField 'String', pushRestUrl, tazPushRestUrl
            buildConfigField 'String', pushRestUrl, tazPushRestUrl
            buildConfigField 'String', errorMail, tazErrorMail
        }
        lmd {
            dimension "publication"
            applicationId "de.thecode.lmd"
            resValue 'string', 'app_name', 'LMd'
            buildConfigField 'String', plistUrl, lmdPlist
            buildConfigField 'String', plistArchiveUrl, lmdPlistArchiv
            buildConfigField 'String', aboUrl, lmdAboUrl
            buildConfigField 'String', checkLoginUrl, lmdCheckLogin
            buildConfigField 'String', resourceUrl, lmdResourceUrl
            buildConfigField 'String', archiveUrl, lmdArchiveUrl
            buildConfigField 'String', pushRestUrl, lmdPushRestUrl
            buildConfigField 'String', errorMail, lmdErrorMail
        }
    }

    applicationVariants.all { variant ->
        def appName = variant.mergedFlavor.resValues.get('app_name').getValue()
        appName = "${appName}"
        if (!variant.buildType.name.equalsIgnoreCase("release")) {
            appName += " " + variant.buildType.name.toUpperCase()
        }
        variant.resValue 'string', 'app_name', appName
        if (variant.buildType.name.equalsIgnoreCase("staging")) {
            variant.getProductFlavors().each { flavor ->
                if (flavor.name.equalsIgnoreCase('taz')) {
                    variant.buildConfigField 'String', plistUrl, tazStagingPlist
                    variant.buildConfigField 'String', plistArchiveUrl, tazStagingPlistArchiv
                    variant.buildConfigField 'String', pushRestUrl, tazStagingPushRestUrl
                } else if (flavor.name.equalsIgnoreCase('lmd')) {
                    variant.buildConfigField 'String', plistUrl, lmdStagingPlist
                    variant.buildConfigField 'String', plistArchiveUrl, lmdStagingPlistArchiv
                    variant.buildConfigField 'String', pushRestUrl, lmdStagingPushRestUrl
                }

            }
        }
    }

    def abiCodes = ['universal': 0, 'armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 4, 'mips64': 5, 'x86': 6, 'x86_64': 7].withDefault {
        0
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def baseAbiVersionCode = abiCodes.get(output.getFilter(com.android.build.OutputFile.ABI))
            output.versionCodeOverride = baseAbiVersionCode * 100000000 + variant.versionCode
            output.versionNameOverride = variant.versionName + " " + abiCodes.find { it.value == baseAbiVersionCode }.key
        }
    }

    productFlavors.all { flavor ->
        def helpDir = new File(project.projectDir, "src/${flavor.name}/assets/help")
        def localZip = new File(project.ext.overheadPath, "temp_${flavor.name}.zip")
        def helpDownloadTaskName = "helpDownloadFile${flavor.name.capitalize()}"
        def helpUnzipTaskName = "helpUnzipFile${flavor.name.capitalize()}"
        def helpDeleteZipTaskName = "helpDeleteZipFile${flavor.name.capitalize()}"
        def helpDeleteDirTaskName = "helpDeleteDir${flavor.name.capitalize()}"
        def downloadTask = task "${helpDownloadTaskName}"(type: Download) {
            sourceUrl = "http://dl.taz.de/down/help/${flavor.name}.zip"
            target = localZip
        }
        def deleteZipTask = task "${helpDeleteZipTaskName}"(type: Delete) {
            delete localZip
        }
        def deleteHelpDirTask = task "${helpDeleteDirTaskName}"(type: Delete) {
            delete helpDir
        }
        def unzipTask = task "${helpUnzipTaskName}"(dependsOn: [downloadTask, deleteHelpDirTask], type: Copy) {
            from zipTree(downloadTask.target)
            into helpDir
            doLast {
                deleteZipTask.execute()
            }
        }
        tasks.preBuild.dependsOn unzipTask
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$supportLibVer"
    implementation "com.android.support:recyclerview-v7:$supportLibVer"
    implementation "com.android.support:design:$supportLibVer"
    implementation "com.android.support:support-v4:$supportLibVer"
    implementation "com.android.support:cardview-v7:$supportLibVer"
    implementation "com.takisoft.fix:preference-v7:$supportLibVer" + ".0"
    implementation "com.takisoft.fix:preference-v7-material:$supportLibVer" + ".0"
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    implementation 'com.googlecode.plist:dd-plist:1.8'
    implementation 'com.commonsware.cwac:provider:0.5.3'
    implementation 'org.greenrobot:eventbus:3.0.0'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    stagingImplementation 'com.facebook.stetho:stetho:1.5.0'
    stagingImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.github.jrejaud:ViewPagerIndicator2:0.0.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.yqritc:recyclerview-flexibledivider:1.2.9'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
    implementation 'net.ypresto.timbertreeutils:timbertreeutils:1.0.0'
    implementation 'com.scottyab:aescrypt:0.0.1'
    implementation 'com.github.matecode:Snacky:1.0.3'
    implementation('com.github.matecode:androiddialog:1.2.34')
//    implementation 'commons-io:commons-io:2.6'
    implementation "com.google.firebase:firebase-core:16.0.3"
    implementation "com.google.firebase:firebase-messaging:17.3.2"
    implementation 'com.evernote:android-job:1.2.6'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
//    implementation "android.arch.lifecycle:common-java8:1.1.1"

    implementation "org.apache.commons:commons-lang3:3.7"
    implementation "commons-io:commons-io:2.5" // Dont update until Android 4 is supported

    implementation 'com.github.bosphere.android-filelogger:filelogger:1.0.6'

//    implementation "com.google.guava:guava:24.1-android"
}

class Download extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}

apply plugin: 'com.google.gms.google-services'
